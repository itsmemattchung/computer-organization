# t0
# to_uppercase
# This function accepts a pointer (to a string) and converts all lowercase letters
# to uppercase, directly manipulating the array -- in place. We do this to avoid:
#  1) managing a heap
#  2) passing in two arguments, forcing the caller to manage a runtime stack for the new string
#
# I'm thinking out loud as I try to implement the difference of set 1 against set 2. We basically want the items that are in A and only in A, while not in B.
# 1001    1111    0010
# 1000    1001    1100
# ----    ----    ----
# 0001    0110    0010
#
#
# 0010        -> 0010
# 1100 -> not -> 0011
#                ----
#                0010

	.data
user_string_1: .space 64
user_string_2: .space 64
adt_string_1: .space 4
adt_string_2: .space 4
adt_string_union: .space 4
adt_string_intersection: .space 4
adt_string_difference: .space 4

str_prompt_1:	.asciiz	"\nInput first string:"
str_prompt_2:	.asciiz	"\nInput second string:"
str_prompt_3:	.asciiz "\nExiting!"
newline_char:	.asciiz "\n"

str_chars_in_bitmap_1:		.asciiz "Set A contains: "
str_chars_in_bitmap_2:		.asciiz "Set B contains: "
str_union_output:		.asciiz "The union of the sets are: "
str_intersection_output:	.asciiz "The intersection of the sets are: "
str_difference_output:		.asciiz "The difference of A - B is: "
char_contains_A:		.asciiz "Does Set A contain the letter 'A':"
char_contains_Q:		.asciiz "Does Set A contain the letter 'Q':"
char_contains_Y:		.asciiz "Does Set A contain the letter 'Y':"
char_contains_Z:		.asciiz "Does Set A contain the letter 'Z':"

	.text
	.globl main
main:
	li 	$v0, 4
	la 	$a0, str_prompt_1
	syscall
	li 	$v0, 8			# Prompt for string sys call
	la	$a0, user_string_1	# Load user_string_1 into argument
	li	$a1, 64
	syscall
	li 	$v0, 4
	la 	$a0, str_prompt_2
	syscall
	li 	$v0, 8			# Prompt for string sys call
	la	$a0, user_string_2	# Load user_string_1 into argument
	li	$a1, 64			 
	syscall
	la	$a0, user_string_1
	jal	to_uppercase
	la	$a0, user_string_1
	la	$a1, adt_string_1
	jal	adt_set
	li	$v0, 4
	la	$a0, str_chars_in_bitmap_1
	syscall
	la	$a0, adt_string_1		# Load bitstring into $a0, the first argument
	jal	print_letters_in_bit_string	# Call print_letters_in_bit_string


	li	$v0, 4
	la	$a0, str_chars_in_bitmap_2
	syscall

adt_bitstring_2:
	la	$a0, user_string_2
	jal	to_uppercase
	la	$a0, user_string_2
	la	$a1, adt_string_2
	jal	adt_set

	la	$a0, adt_string_2		# Load bitstring into $a0, the first argument
	jal	print_letters_in_bit_string	# Call print_letters_in_bit_string

print_union:
	li	$v0, 4
	la	$a0, str_union_output
	syscall
	la	$a0, adt_string_1			
	la	$a1, adt_string_2
	la	$a2, adt_string_union
	jal	union
	move	$a0, $a2			# Move the union bit-string address into $a0, before calling print_letters_in_bit_string
	jal	print_letters_in_bit_string

print_intersection:
	li	$v0, 4
	la	$a0, str_intersection_output
	syscall
	la	$a0, adt_string_1
	la	$a1, adt_string_2
	la	$a2, adt_string_intersection
	jal	intersection
	move	$a0, $a2			# Move the intersection bit-string address into $a0, before calling print_letters_in_bit_string
	jal	print_letters_in_bit_string

print_difference:
	li	$v0, 4
	la	$a0, str_difference_output
	syscall
	la	$a0, adt_string_1
	la	$a1, adt_string_2
	la	$a2, adt_string_difference
	jal	difference
	move	$a0, $a2			# Move the intersection bit-string address into $a0, before calling print_letters_in_bit_string
	jal	print_letters_in_bit_string

print_set_a_contains_a:
	li	$v0, 4
	la	$a0, char_contains_A
	syscall
	li	$a0, 65				# Load ascii character 'A' as first argument
	la	$a1, adt_string_1		# Load bit string pointer (i.e. memory address) as second argument
	jal	contains
	move	$a0, $v0			# Move return value from contains() into $a0
	li	$v0, 1				# Load system call -- print_int() -- into $v0
	syscall
	li	$v0, 4
	la	$a0, newline_char
	syscall

print_set_a_contains_q:
	li	$v0, 4
	la	$a0, char_contains_Q
	syscall
	li	$a0, 81				# Load ascii character 'Q' as first argument
	la	$a1, adt_string_1		# Load bit string pointer (i.e. memory address) as second argument
	jal	contains
	move	$a0, $v0			# Move return value from contains() into $a0
	li	$v0, 1				# Load system call -- print_int() -- into $v0
	syscall
	li	$v0, 4
	la	$a0, newline_char
	syscall

print_set_a_contains_y:
	li	$v0, 4
	la	$a0, char_contains_Y
	syscall
	li	$a0, 89				# Load ascii character 'Q' as first argument
	la	$a1, adt_string_1		# Load bit string pointer (i.e. memory address) as second argument
	jal	contains
	move	$a0, $v0			# Move return value from contains() into $a0
	li	$v0, 1				# Load system call -- print_int() -- into $v0
	syscall
	li	$v0, 4
	la	$a0, newline_char
	syscall

print_set_a_contains_z:
	li	$v0, 4
	la	$a0, char_contains_Z
	syscall
	li	$a0, 90				# Load ascii character 'Q' as first argument
	la	$a1, adt_string_1		# Load bit string pointer (i.e. memory address) as second argument
	jal	contains
	move	$a0, $v0			# Move return value from contains() into $a0
	li	$v0, 1				# Load system call -- print_int() -- into $v0
	syscall
	li	$v0, 4
	la	$a0, newline_char
	syscall

print_exit:
	li 	$v0, 4				# Load print system call
	la 	$a0, str_prompt_3		# Print "exit"
	syscall
	li $v0, 10			# Load exit() system call
	syscall				# Exit to the program


adt_set:
	move	$t0, $a0
	move	$t6, $a1					# Load the address of the 32-bit string into $t6
	lw	$t1, 0($t6)					# Load word (32-bit) into $t1
	li	$t2, 0						# Initialize i (i.e. index) to 0

adt_set_while_not_end_of_string:
	add	$t3, $t2, $t0					# Calculate address
	lb	$t4, 0($t3)					# Load byte (not word) into $t4
	beq	$t4, 0, adt_set_end_while_not_end_of_string	# If null terminator (i.e. 0), then jump to end of while loop

adt_set_if_alphabetical_char:
	blt 	$t4, 65, adt_set_end_if_aphabetical_char	# Check if char less than 'A'
	bgt 	$t4, 90, adt_set_end_if_aphabetical_char	# Check if char is greater than 'Z'
	sub	$t4, $t4, 65					# Subtract ascii character by 65 - this is the amount we are going to shift left by
	li	$t5, 1						# $t5 used for bit masking
	sllv	$t5, $t5, $t4					# Position the letter into the correct position, determined by $t4
	or	$t1, $t1, $t5					# Or the adt set against the newly positioned character

adt_set_end_if_aphabetical_char:
	add $t0, $t0, 1
	j adt_set_while_not_end_of_string

adt_set_end_while_not_end_of_string:
	sw	$t1, 0($t6)					# Store 32-bit bit map into $t6
	jr $ra

to_uppercase:
	move	$t0, $a0				# Save array address from $a0 to $1
	li	$t1, 0					# Initialize i (i.e. index) to 0
while_not_end_of_string:
	add	$t2, $t1, $t0				# Calculate address
	lb	$t3, 0($t2)				# Load byte (not word) into $t2
	beq	$t3, 0, end_while_not_end_of_string	# If null terminator (i.e. 0), then jump to end of while loop

if_alphabetical_char:
	blt $t3, 97, end_if_aphabetical_char		# Check if char less than 'a'
	bgt $t3, 122, end_if_aphabetical_char		# Check if char is greater than 'z'

uppercase_char:
	andi	$t4, $t3, 0xDF				# Convert character to uppercase, regardless
	sb	$t4, 0($t2)				# Store byte into original position

end_if_aphabetical_char:
	add $t1, $t1, 1
	j while_not_end_of_string
end_while_not_end_of_string:
	jr $ra

print_letters_in_bit_string:
	add	$sp, $sp, -36				# Store the ret and allocate 32 bytes for ascii string
	sw	$ra, 0($sp)				# Store retunr address on the stack
	move	$t6, $sp				# Copy address of $rsp to $t6, the register holding address of ascii string
	add	$t6, $t6, 4				# Load address of the ascii string that we'll allocate for the print call
	lw	$t0, 0($a0)				# Load contents of bit-map (in $a0) into $t0
	li	$t1, 0					# Counter for bit string
	li	$t2, 0					# Counter to hold the offset for the new ascii string
	move	$t5, $t6				# Initialize $t5 to value of $t6, the starting address for the ascii string. Should fix bug when no string inputted

while_not_end_of_bitstring:
	beq	$t1, 25, end_while_not_end_of_bitstring # Once we reach last character, break out of while loop
	srlv	$t3, $t0, $t1				# Shift right by counter
	and	$t3, $t3, 0x01				# and the newly shifted bitstring by 1

if_char_is_set:
	beq	$t3, 0, end_if_char_is_set
	add	$t4, $t1, 65				# Add 65 (i.e. first position for 'A') to $t1
	add	$t5, $t2, $t6				# Calculate address for char position in ascii string
	sb	$t4, 0($t5)				# Store ascii character into new ascii string
	add	$t2, $t2, 1				# Bump ascii string offset counter by 1

end_if_char_is_set:
	add $t1, $t1, 1					# Increment bit-string counter by 1
	j while_not_end_of_bitstring

end_while_not_end_of_bitstring:
	li	$t7, 0x0A				# Store new line
	sb	$t7, 1($t5)				# Store newline in penultimate position
	li	$t7, 0					# Load null terminator (i.e. 0)
	sb	$t7, 2($t5)				# Store null terminator in last position
	lw	$ra, 0($sp)				# restore the return
	add	$sp, $sp, 36
	li 	$v0, 4					# Load print system call
	move	$a0, $t6				# Copy string into $a0
	syscall
	jr	$ra

union:
	move	$t1, $a0				# Move pointer of first bit-string to $t1
	move	$t2, $a1				# Move point of second bitstring to $t2
	move	$t3, $a2				# Move pointer of third bit string, the union, to $t3
load_first_bit_string:
	lw	$t4, 0($t1)				# Load the 32-bit bit map into $t4
load_second_bit_string:
	lw	$t5, 0($t2)				# Load the 32-bit bit map into $t5
bitmap1_or_bitmap2:
	or	$t6, $t4, $t5				# bitmap1 | bitmap2
store_union:
	sw	$t6, 0($t3)				# Store the new bitmap that's been OR'd back to third pointer that was passed in
union_end:
	jr $ra

intersection:
	move	$t1, $a0				# Move pointer of first bit-string to $t1
	move	$t2, $a1				# Move point of second bitstring to $t2
	move	$t3, $a2				# Move pointer of third bit string, the union, to $t3
intersection_load_first_bit_string:
	lw	$t4, 0($t1)				# Load the 32-bit bit map into $t4
intersection_load_second_bit_string:
	lw	$t5, 0($t2)				# Load the 32-bit bit map into $t5
bitmap1_and_bitmap2:
	and	$t6, $t4, $t5				# bitmap1 | bitmap2
store_intersection:
	sw	$t6, 0($t3)				# Store the new bitmap that's been OR'd back to third pointer that was passed in
intersection_end:
	jr $ra

difference:
	move	$t1, $a0				# Move pointer of first bit-string to $t1
	move	$t2, $a1				# Move point of second bitstring to $t2
	move	$t3, $a2				# Move pointer of third bit string, the union, to $t3
difference_load_first_bit_string:
	lw	$t4, 0($t1)				# Load the 32-bit bit map into $t4
difference_load_second_bit_string:
	lw	$t5, 0($t2)				# Load the 32-bit bit map into $t5
	not	$t5, $t5				# Invert all bits before AND (next instruction)
bitmap1_difference_bitmap2:
	and	$t6, $t4, $t5				# bitmap1 | bitmap2
store_difference:
	sw	$t6, 0($t3)				# Store the new bitmap that's been OR'd back to third pointer that was passed in
difference_end:
	jr $ra

contains:
	move	$t0, $a0				# Move first argument, the char to check in the bitstring
	move 	$t1, $a1				# Move second argument, the bitstring pointer
	lw	$t2, 0($t1)				# Load the 32-bit string into $t2
	li	$t3, 1					# This is the register we use to run and against
contains_convert_char_to_bitstring_position:
	sub	$t0, $t0, 65
shift_bitmap_by_position:
	srlv	$t2, $t2, $t0				# Shift the bitmap into position lining up with ASCII character
check_if_char_in_set:
	and	$v0, $t2, $t3				# Run AND, checking if char is in bitmap
	jr	$ra
