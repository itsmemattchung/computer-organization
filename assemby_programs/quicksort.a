# Author: Matt Chung
# This program performs a quicksort against integer numbers, implementing the partition method.
#
# == quicksort ==
# We need to store two variables that get preserved across calls: low and high
# In addition,we need need to store the value 'partition'
# s0 = array (address)
# s1 = low
# s2 = high
# s3 = pivot
#
# == parition ==
# since we need to preserve the value of the variables within the function
# as we call the 'swap()' function, we'll need to store practically all the variables
# in registers $s0-$s7, registers preserved across method calls
# $s0 = left
# $s1 = right
# $s2 = array
# $s3 = pivot
# $s4 = array[right] (value)
#
# However, there are variables that need not be preserved, such as:
# $t0 = array[low] (address)
# $t1 = array[left] (address)
# $t2 = array[left] (value)
# $t2 = array[right] (value)
# $t3 = array[right] (address) -- since I'm unsure whether $a1 gets preserved across the call
# $t4 = address offset for array[right]
# $t5 = temporary variable for swap, although I'd like to eliminate this eventually and reuse $t1, but only after verifying the program works and after verifying
# that I'm not overwriting a referenced variable
# $t6 = temporary variable for swap function
# $t7 = temporary for final swap
# $t8 = temporary for final swap



	    .data
numbers:	.word 8, 2, 7, 10, 5, 6, 3, 4, 9, 3, 1
length:		.word 11
	    .text
	    .globl main
main:
	la $a0, numbers		# Load the address numbers into $a0, the first argument
	li $a1, 0		# Load 0 into $a, the second argument
	lw $t0, length		# Load the length of the array into temporary register $t0
	sub $a2, $t0, 1		# Subtract 1 from the length of the array and store into $a3, the first argument
	jal quicksort
	li $v0, 10
	syscall

quicksort:

quicksort_if:

	bge $a1, $a2, quicksort_end_if	# CHeck if low < high

quicksort_body:
	addi, $sp, $sp, -24	# Update the stack pointer, allocating enough room for savin the 6 registers
	sw $ra, 0($sp)		# Store return address on the stack
	sw $s0, 4($sp)		# Store $s1 on the stack
	sw $s1, 8($sp)		# Store $s2 on the stack
	sw $s2, 12($sp)		# Store $s3 on the stack
	sw $s3, 16($sp)		# Store $s3 on the stack
	sw $s4, 20($sp)		# Store $s3 on the stack
	move $s0, $a0		# Store $a0 (address of array) into $s0
	move $s1, $a1		# Store $a1 (low) into $s1
	move $s2, $a2		# Store $a2 (high) into $s2
	jal partition		# Call the subprogram partition
	move $s4, $v0		# Store pivot -1 into $a2
	move $a0, $s0		# Store $s0, array, into $a0
	move $a1, $s1		# Store $s1, low, into $a1
	move $a2, $s4		# Store pivot position in $a2
	sub $a2, $a2, 1		# Subtract 1 from $a2 (i.e. pivot - 1)
	jal quicksort		# First call to quicksort (i.e. quickSort(array, low, pivotPosition - 1)
	move $a0, $s0		# Store array into $a0
	move $a1, $s4		# Move pivot into $a1
	add $a1, $a1, 1		# Add 1 to $a1 (i.e. pivot + 1)
	move $a2, $s2		# Move high (i.e. $s2) into $a2
	jal quicksort		# Second call to quicksort (i.e. quickSort(array, pivotPosition + 1, high)
	lw $ra, 0($sp)		# Restore the return address from the stack and back into $ra
	lw $s0, 4($sp)		# Restore $s0 from the stack
	lw $s1, 8($sp)		# Restore $s1 from the stack
	lw $s2, 12($sp)		# Restore $s2 from the stack
	lw $s3, 16($sp)		# Restore $s2 from the stack
	lw $s4, 20($sp)		# Restore $s2 from the stack
	addi $sp, $sp, 24	# Pop the call frame
	jr $ra
end_quicksort_body:

quicksort_end_if:
	jr $ra

partition:
partition_init:
	addi $sp, $sp, -28				# Update the stack pointer, allocating enough room for savin the 6 registers
	sw $ra, 0($sp)					# Store the return address (i.e. $ra) on the stack
	sw $s0, 4($sp)					# Store $s0 on the stack
	sw $s1, 8($sp)					# Store $s1 on the stack
	sw $s2, 12($sp)					# Store $s2 on the stack
	sw $s3, 16($sp)					# Store $s3 on the stack
	sw $s4, 20($sp)					# Store $s4 on the stack
	sw $s5, 24($sp)					# Store $s5 on the stack
	move $s5, $a1					# Save value of low
	move $s0, $a1					# Store value of low to left, $s0
	move $s1, $a2					# Store value of high to right, $s1
	move $s2, $a0					# Store address of array in $s2
	mul $s3, $s0, 4					# Caculate offset of array[low] into $s3
	add $s3, $s2, $s3				# Add offset to array
	lw $s3, 0($s3)					# Store the value of array[low] into pivot

while_left_lt_right:
	bge $s0, $s1, end_while_left_lt_right

while_array_right_gt_pivot:
	mul $t3, $s1, 4					# Calculate offset of array[right] ; definitely an issue with this while_array. this should never allow right to be 0
	add $t3, $s2, $t3				# Calculate memory address of array[right]
	lw $s4, 0($t3)					# Load array[right] into $s4
	ble $s4, $s3, end_while_array_right_gt_pivot	# Check if array[right] > pivot 
	sub $s1, $s1, 1					# Subtract right by 1
	j while_array_right_gt_pivot
	
end_while_array_right_gt_pivot:

while_left_lt_right_and_array_left_lte_pivot:
	bge $s0, $s1, end_while_left_lt_right_and_array_left_lte_pivot	# Jump to end of loop if !(left < right)
	mul $t1, $s0, 4							# Calculate offset for array[left]
	add $t1, $s2, $t1						# Calculate memory address of array[left]
	lw  $t2, 0($t1)							# Load array[left] into $t2
	bgt $t2, $s3, end_while_left_lt_right_and_array_left_lte_pivot	# Break out of loop if !array[left] <= pivot
	addi $s0, $s0, 1						# +1 to left
	j while_left_lt_right_and_array_left_lte_pivot			# return to loop

end_while_left_lt_right_and_array_left_lte_pivot:

if_left_lt_right:
	bge $s0, $s1, end_if_left_lt_right				# jump to end_if if !(left < right)
	move $a0, $t1							# store address of array[left] to $a0
	move $a1, $t3							# store address of array[right] to $a1
	jal swap

end_if_left_lt_right:
	j while_left_lt_right

end_while_left_lt_right:

move_pivot_to_final_position:
	mul $t7, $s1, 4							# Calculate the offset for array[right] and store into $t7
	add $t7, $s2, $t7						# Calculate memory address of array[right] and store into $t7
	lw $t8, 0($t7)							# Load the value of array[right] into $t8
	mul $t9, $s5, 4							# Calculate offset of array[low] into $t8
	add $t9, $s2, $t9						# Store address of array[low] into $t8
	sw $t8, 0($t9)							# Save value of array[right] into array[low] (i.e. array[low] = array[right])
	sw $s3, 0($t7)							# Store pivot at array[right]
	move $v0, $s1							# Move right into $v0, the return value
	lw $ra, 0($sp)							# Load $ra previously saved on stack into $ra
	lw $s0, 4($sp)							# Restore $s0 from the stack
	lw $s1, 8($sp)							# Restore $s1 from the stack
	lw $s2, 12($sp)							# Restore $s2 from the stack
	lw $s3, 16($sp)							# Restore $s3 from the stack
	lw $s4, 20($sp)							# Restore $s4 from the stack
	lw $s5, 24($sp)							# Restore $s5 from the stack
	addi $sp, $sp, 28						# POPPPPP call frame
	jr $ra								# Return $ra from partition method
end_partition:

swap:
	move $t5, $a0							# Store memory address of A into $t5
	lw $t5, 0($t5)							# Load value into $t5
	move $t6, $a1							# Store memory address of B into $t6
	lw $t6, 0($t6)							# Load value into $t6
	sw $t5, 0($a1)							# Store A into memory address of B
	sw $t6, 0($a0)							# Store B into memory address of A
	jr $ra								# Jump to the instruction stored in $ra
